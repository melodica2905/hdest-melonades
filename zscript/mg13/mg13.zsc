/*
"MG13" Hand Grenades

Found in an old ACME (Armed CoMbat Explosives) Manufacturing Ltd. warehouse, 
stashed inside large boxes labeled "DANGER", the MG13 was ment as a new, revolutionary
hand grenade, providing enough explosive power to level small buildings, destroy light
vehicles, and able to be thrown and carried by just about any soldier on the field.

In their infinite wistom, they manufactured these in large quantities, way before
any testings were actually done with them. Concerns quickly arose after the soldier
tasked with throwing the grenade to test it was barely able to throw it a few feet
forward, and those (alongside the people holding them) were quickly erased as the grenade
detonated, levelling the entire building and killing atleast 80% of its personel.
*/

const HDLD_GRE_MG13="mg3";
const ENC_MG13GRENADE=133;

class HDMG13Grenades:HDGrenadethrower{
	default{
		weapon.selectionorder 1000;
		weapon.slotpriority 0;
		weapon.slotnumber 0;
		tag "MG13 Grenades";
		hdgrenadethrower.ammotype "HDMG13GrenadeAmmo";
		hdgrenadethrower.throwtype "HDMG13Grenade";
		hdgrenadethrower.spoontype "HDMG13Spoon";
		hdgrenadethrower.wiretype "MG13TripwireGrenade";
		inventory.icon "MG13A0";
	}
	
	override string,double getpickupsprite(){return "MG13A0",0.8;}
	override void DrawHUDStuff(HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl){
		if(sb.hudlevel==1){
			sb.drawimage(
				(weaponstatus[0]&FRAGF_PINOUT)?"MG13F0":"MG13A0",
				(-52,-4),sb.DI_SCREEN_CENTER_BOTTOM,scale:(0.8,0.8)
			);
			sb.drawnum(hpl.countinv("HDMG13GrenadeAmmo"),-45,-8,sb.DI_SCREEN_CENTER_BOTTOM);
		}
		sb.drawwepnum(
			hpl.countinv("HDMG13GrenadeAmmo"),
			(HDCONST_MAXPOCKETSPACE/ENC_FRAG)
		);
		sb.drawwepnum(hdw.weaponstatus[FRAGS_FORCE],50,posy:-10,alwaysprecise:true);
		if(!(hdw.weaponstatus[0]&FRAGF_SPOONOFF)){
			sb.drawrect(-21,-19,5,4);
			if(!(hdw.weaponstatus[0]&FRAGF_PINOUT))sb.drawrect(-25,-18,3,2);
		}else{
			int timer=hdw.weaponstatus[FRAGS_TIMER];
			if(timer%3)sb.drawwepnum(140-timer,140,posy:-15,alwaysprecise:true);
		}
	}
	
	override void ForceBasicAmmo(){
		owner.A_SetInventory("HDMG13GrenadeAmmo",1);
	}
	
	states
	{
		deselectinstant:
			TNT1 A -1 A_TakeInventory("HDMG13Grenades", 1);
			stop;
	}
}

class MG13TripwireGrenade:Tripwire{
	default{
		weapon.selectionorder 1001;
		tripwire.ammotype "HDMG13GrenadeAmmo";
		tripwire.throwtype "MG13TrippingFrag";
		tripwire.spoontype "HDMG13Spoon";
		tripwire.weptype "HDMG13Grenades";
	}
	override void DrawHUDStuff(HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl){
		if(sb.hudlevel==1){
			sb.drawimage("MG13A0",(-52,-4),sb.DI_SCREEN_CENTER_BOTTOM,scale:(0.8,0.8));
			sb.drawnum(hpl.countinv("HDMG13GrenadeAmmo"),-45,-8,sb.DI_SCREEN_CENTER_BOTTOM);
		}
		sb.drawwepnum(
			hpl.countinv("HDMG13GrenadeAmmo"),
			(ENC_FRAG/HDCONST_MAXPOCKETSPACE)
		);
		sb.drawwepnum(hdw.weaponstatus[FRAGS_FORCE],50,posy:-10,alwaysprecise:true);
		if(!(hdw.weaponstatus[0]&FRAGF_SPOONOFF)){
			sb.drawrect(-21,-19,5,4);
			if(!(hdw.weaponstatus[0]&FRAGF_PINOUT))sb.drawrect(-25,-18,3,2);
		}else{
			int timer=hdw.weaponstatus[FRAGS_TIMER];
			if(timer%3)sb.drawwepnum(140-timer,140,posy:-15,alwaysprecise:true);
		}
	}
}

class MG13TrippingFrag:TrippingGrenade{
	default{
		//$Category "Misc/Hideous Destructor/Traps"
		//$Title "Tripwire Grenade"
		//$Sprite "FRAGA0"

		scale 0.65;
		trippinggrenade.rollertype "HDMG13GrenadeRoller";
		trippinggrenade.spoontype "HDMG13Spoon";
		trippinggrenade.droptype "HDMG13GrenadeAmmo";
		hdupk.pickuptype "HDMG13GrenadeAmmo";
	}
	override void postbeginplay(){
		super.postbeginplay();
		pickupmessage=getdefaultbytype("HDMG13GrenadeAmmo").pickupmessage();
	}
	states{
	spawn:
		MG13 A 1 nodelay A_TrackStuckHeight();
		wait;
	}
}

class HDMG13GrenadeRoller:HDFragGrenadeRoller{
	default{
		scale 0.65;
		obituary "%o was fragged by %k.";
		radiusdamagefactor 0.04;pushfactor 1.4;maxstepheight 2;mass 500;
	}
	states{
	spawn:
		MG13 A 0 nodelay{
			HDMobAI.Frighten(self,512);
		}
	spawn2:
		#### A 0 A_ScaleVelocity(0.2);
		#### BCD 2{
			if(abs(vel.z-keeprolling.z)>10)A_StartSound("misc/fragknock",CHAN_BODY);
			else if(floorz>=pos.z)A_StartSound("misc/fragroll");
			keeprolling=vel;
			if(abs(vel.x)<0.4 && abs(vel.y)<0.4) setstatelabel("death");
		}goto spawn2+1;
	bounce:
		---- A 0{
			bmissile=false;
			vel*=0.3;
		}goto spawn2;
	death:
		---- A 2{
			if(abs(vel.z-keeprolling.z)>3){
				A_StartSound("misc/fragknock",CHAN_BODY);
				keeprolling=vel;
			}
			if(abs(vel.x)>0.4 || abs(vel.y)>0.4) setstatelabel("spawn");
		}wait;
	destroy:
		TNT1 A 3{
			bsolid=false;bpushable=false;bmissile=false;bnointeraction=true;bshootable=false;
			A_HDBlast(
				512,20000,256,"slashing",
				pushradius:512,pushamount:2560,
				fragradius: HDCONST_ONEMETRE*50, fragtype: "HDB_frag",
				immolateradius:512,immolateamount:-500,immolatechance:90
			);
			A_SpawnChunksFrags("HDB_frag", 360, 1.);
			//A_SpawnChunksFrags("HDB_frag", 360, 2.);
			DistantQuaker.Quake(self,4,35,512,10);
			DistantQuaker.Quake(self,4,35,512,10);
			A_StartSound("mg13/explode",CHAN_AUTO);
			A_AlertMonsters();
			actor xpl=spawn("WallChunker",self.pos-(0,0,1),ALLOW_REPLACE);
				xpl.target=target;xpl.master=master;xpl.stamina=stamina;
			xpl=spawn("HDSilentExplosion",self.pos-(0,0,1),ALLOW_REPLACE);
				xpl.target=target;xpl.master=master;xpl.stamina=stamina;
			A_SpawnChunks("BigWallChunk",14,4,12);
			distantnoise.make(self,"world/rocketfar");
			
			A_SpawnChunks("HDMG13Smoke", 20, 10, 15);
			A_SpawnItemEx("HDSilentExplosionBigger2",0,0,0, flags: SXF_NOCHECKPOSITION | SXF_ORIGINATOR | SXF_SETMASTER);
			for(int i; i < 8; i++)
			{
				A_SetAngle(angle+45);
				A_SpawnItemEx("HDSilentExplosionBigger",256+random(-32,32),random(-32,32),random(0, 64), flags: SXF_NOCHECKPOSITION | SXF_ORIGINATOR | SXF_SETMASTER );
				A_SpawnItemEx("HDSilentExplosionBigger",128+random(-32,32),random(-32,32),random(0, 64), flags: SXF_NOCHECKPOSITION | SXF_ORIGINATOR | SXF_SETMASTER );
			}
		}
		stop;
	}
}
class HDMG13Grenade:HDFragGrenade{
	default{
		scale 0.65;
		obituary "%o was fragged by %k.";
		hdfraggrenade.rollertype "HDMG13GrenadeRoller";
		Mass 1500;
	}
	states
	{
	spawn:
		TNT1 A 0 NODELAY A_ScaleVelocity(0.3);
		MG13 BCD 2 ;
		Goto Spawn+1;
	}
}

class HDMG13GrenadeAmmo:HDAmmo{
	default{
		//+forcexybillboard
		inventory.icon "MG13A0";
		inventory.amount 1;
		scale 0.65;
		inventory.maxamount 50;
		inventory.pickupmessage "Picked up an MG13 Grenade.";
		inventory.pickupsound "weapons/pocket";
		tag "MG13 Grenades";
		hdpickup.refid HDLD_GRE_MG13;
		hdpickup.bulk ENC_MG13GRENADE;
		+INVENTORY.KEEPDEPLETED
	}
	override bool IsUsed(){return true;}
	override void AttachToOwner(Actor user)
	{
		user.GiveInventory("HDMG13Grenades", 1);
		super.AttachToOwner(user);
	}
	override void DetachFromOwner()
	{
		if(!(owner.player.ReadyWeapon is "HDMG13Grenades"))
		{
			TakeInventory("HDMG13Grenades", 1);
		}
		super.DetachFromOwner();
	}
	states{
	spawn:
		MG13 A -1;stop;
	}
}

class HDMG13Spoon : HDFragSpoon
{
	default
	{
		Scale 0.65;
	}
}

class MG13GrenadeP:HDUPK{
	default{
		//+forcexybillboard
		scale 0.65;height 3;radius 3;
		hdupk.amount 1;
		hdupk.pickuptype "HDMG13GrenadeAmmo";
		hdupk.pickupmessage "Picked up an MG13 Grenade.";
		hdupk.pickupsound "weapons/rifleclick2";
		stamina 1;
	}
	override void postbeginplay(){
		super.postbeginplay();
		pickupmessage=getdefaultbytype("HDMG13GrenadeAmmo").pickupmessage();
	}
	states{
	spawn:
		MG13 A -1;
	}
}

class HDSilentExplosion : HDExplosion
{
	default
	{
		deathsound "none";
	}
}

class HDSilentExplosionBigger : HDExplosion
{
	default
	{
		scale 3.5;
		deathsound "none";
		//-NOINTERACTION
	}
	states
	{
	spawn:
		MISL B 0 NODELAY A_JumpIfTargetInLOS("death", 0, JLOSF_CHECKMASTER | JLOSF_CLOSENOJUMP, dist_close: 64);
	begone:
		TNT1 A 1;// A_SpawnItemEx("HDMG13Smoke", 0,0,0, vel.x+frandom(-2,2),vel.y+frandom(-2,2),vel.z,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
		stop;
	death:
		MISL B 0{
			if(max(abs(pos.x),abs(pos.y),abs(pos.z))>=32768){destroy();return;}
			vel.z+=2;
			A_StartSound(deathsound,CHAN_BODY);
			let xxx=spawn("HDExplosionLight",pos);
			xxx.target=self;
		}
		MISL BBBB 0 A_SpawnItemEx("ParticleWhiteSmall", 0,0,0, vel.x+random(-2,2),vel.y+random(-2,2),vel.z,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
		MISL BB 0 A_SpawnItemEx("HDMG13Smoke", 0,0,0, vel.x+frandom(-2,2),vel.y+frandom(-2,2),vel.z,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
		MISL B 0 A_Jump(256,"fade");
	fade:
		MISL B 3 A_FadeOut(0.1);
		MISL C 3 A_FadeOut(0.2);
		MISL DD 3 A_FadeOut(0.2);
		TNT1 A 20;
		stop;
	}
}

class HDSilentExplosionBigger2 : HDExplosion
{
	default
	{
		yscale 4.5;
		xscale 5.0;
		deathsound "none";
	}
	states
	{
	spawn:
	death:
		MISL B 0{
			if(max(abs(pos.x),abs(pos.y),abs(pos.z))>=32768){destroy();return;}
			vel.z+=2;
			A_StartSound(deathsound,CHAN_BODY);
			let xxx=spawn("HDExplosionLight",pos);
			xxx.target=self;
		}
		MISL BBBB 0 A_SpawnItemEx("ParticleWhiteSmall", 0,0,0, vel.x+random(-2,2),vel.y+random(-2,2),vel.z,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
		MISL BB 0 A_SpawnItemEx("HDMG13Smoke", 0,0,0, vel.x+frandom(-2,2),vel.y+frandom(-2,2),vel.z,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
		MISL B 0 A_Jump(256,"fade");
	fade:
		MG3X A 2 A_FadeOut(0.1);
		MG3X B 2 A_FadeOut(0.1);
		MG3X CDE 2 A_FadeOut(0.1);
		TNT1 A 20;
		stop;
	}
}

class HDMG13Smoke : HDSmoke
{
	default{
		scale 2.0;gravity 0.05;alpha 0.7;
		hdpuff.fadeafter 7;
		hdpuff.decel 0.95;
		hdpuff.fade 0.99;
		hdpuff.grow 0.07;
		hdpuff.minalpha 0.005;
	}
}